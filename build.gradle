buildscript {
    ext.kotlin_version = '1.2.31' // Required for Kotlin integration
    ext.spring_boot_version = '2.0.0.RELEASE'
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" // Required for Kotlin integration
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.19.2') //Needed for building docker image
    }
}

group = 'com.rdebokx'

apply plugin: 'kotlin' // Required for Kotlin integration
apply plugin: "kotlin-spring" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

repositories {
    mavenCentral()
}

docker {
    name "${project.group}/${jar.baseName}"
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

jar {
    baseName = 'pipeline-showcase'
    version = '0.1.0'
}

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version" // Required for Kotlin integration
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'com.typesafe.akka:akka-actor_2.12:2.5.3'
    testCompile 'com.typesafe.akka:akka-testkit_2.12:2.5.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}